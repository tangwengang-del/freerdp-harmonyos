plugins {
    id 'com.gladed.androidgitversion' version '0.4.14'
}

androidGitVersion {
    abis = ['armeabi':1, 'armeabi-v7a':2, 'arm64-v8a':3, 'mips':5, 'mips64':6, 'x86':8, 'x86_64':9 ]
    prefix ''
}

println 'Version Name: ' + androidGitVersion.name()
println 'Version Code: ' + androidGitVersion.code()

apply plugin: 'com.android.application'
android {
    namespace = "com.freerdp.afreerdp"
    compileSdkVersion = rootProject.ext.compileApi
    buildToolsVersion = rootProject.ext.toolsVersion

    defaultConfig {
        applicationId "com.freerdp.afreerdp"
        minSdkVersion rootProject.ext.minApi
        targetSdkVersion rootProject.ext.targetApi
        vectorDrawables.useSupportLibrary = true
        versionName = androidGitVersion.name() ?: "3.18.0"
        versionCode = androidGitVersion.code() > 0 ? androidGitVersion.code() : 318000
        
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'  // 支持三架构
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeType "jks"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            jniDebuggable true
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }
    
    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

android.applicationVariants.all { variant ->
    variant.assembleProvider.get().doLast {
        variant.outputs.all { output ->
            def outputDir = new File(project.buildDir, "outputs/apk/${variant.name}")
            outputDir.mkdirs()
            
            def sourceFile = output.outputFile
            def destFile = new File(outputDir, sourceFile.name)
            
            if (sourceFile.exists()) {
                copy {
                    from sourceFile
                    into outputDir
                }
                println "APK copied to: ${destFile.absolutePath}"
            }
        }
    }
}

dependencies {
    implementation project(':freeRDPCore')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}
